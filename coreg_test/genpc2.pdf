procedure
parm out type=(string,99)
parm pixdeg type=real default=3600.0
parm vpix type=real default=30.0
parm hpix type=real default=30.0
parm lon type=real
parm lat type=real
parm rastypstr type=string default="1(RasterPixelIsArea)" +
   valid=("1(RasterPixelIsArea)","2(RasterPixelIsPoint)")
parm aspect string valid=("geographic") default="geographic"

local ilon int
local ilat int
local mlon real
local mlat real
local eastp1 real
local northm1 real
local mpixdeg real
local mpixhoriz real
body
let ilon = $fix(&lon)
let mlon = $float(ilon)
let ilat = $fix(&lat)
let mlat = $float(ilat)
let eastp1 = mlon+1.0
let northm1 = mlat-1.0

let mpixdeg = pixdeg
if (vpix<>30.0)
   let mpixdeg = 108000.0/&vpix
   write "resetting vpixdeg &mpixdeg"
end-if

let mpixhoriz = mpixdeg
if (hpix<>vpix)
   let mpixhoriz = 108000.0/&hpix
   write "resetting hpixdeg &mpixhoriz"
end-if

gtgen out=&out 'tiecnvrt 'rectfit +
   geotiff=("ModelTiePointTag=(0,0,0,&mlon,&mlat,0.0)", +
   "ModelTiePointTag=(0,&mpixdeg,0,&mlon,&northm1,0.0)", +
   "ModelTiePointTag=(&mpixhoriz,0,0,&eastp1,&mlat,0.0)", +
   "GTModelTypeGeoKey=2(ModelTypeGeographic)", +
   "GeogAngularUnitsGeoKey=9102(Angular_Degree)", +
   "GeographicTypeGeoKey=4326(GCS_WGS_84)", +
   "GTRasterTypeGeoKey=&rastypstr", +
   "GeogEllipsoidGeoKey=7030(Ellipse_WGS84)")

end-proc
